#!/usr/bin/env perl

use strict;
use warnings;

use FindBin;

# Within the repo, it's bin/../lib. Elsewhere could be bin/lib.
use lib "$FindBin::Bin/lib";        # bin/lib/
use lib "$FindBin::Bin/../lib";     # bin/../lib/

use File::Slurp;
use IO::Prompt::Tiny 'prompt';
use List::MoreUtils  'uniq';

use Word;
use WordList;

my @dictionary = (
#    '/usr/share/dict/words',
#    "$FindBin::Bin/../lib/dict/2of12inf.txt",
    "$FindBin::Bin/../lib/dict/enable1.txt"
);

my @wl = map {
    WordList->new(words => [
        map {(m/^([a-z]{3,})\b/i && Word->new(word => lc $1)) || ()}
        read_file($_, chomp => 1)
    ])
} @dictionary;

while (1) {
    my $letters = prompt 'Enter a set of letters (empty to exit)', '';
    last unless length $letters;
    my @words = sort {length $a <=> length $b || $a cmp $b}
                uniq map {@{$_->words_spelled($letters)}} @wl;
    while (@words) {
        my ($left, $right) = (shift(@words), shift(@words));
        printf "%10s\t%10s\n",
            defined $left  ? $left  : '',
            defined $right ? $right : '';
    }
}
