#!/usr/bin/env perl

use strict;
use warnings;

use FindBin;

# Within the repo, it's bin/../lib. Elsewhere could be bin/lib.
use lib "$FindBin::Bin/lib";        # bin/lib/
use lib "$FindBin::Bin/../lib";     # bin/../lib/

use File::Slurp;
use IO::Prompt::Tiny 'prompt';
use List::MoreUtils  'uniq';
use MCE::Map Sereal => 1;
use Word;
use WordList;

my @dicts = (
#    '/usr/share/dict/words',
#    "$FindBin::Bin/../lib/dict/2of12inf.txt",
    "$FindBin::Bin/../lib/dict/enable1.txt"
);

my @wl = WordList->new(words => [
    uniq map {
        mce_map_f {
            (lc($_) =~ m/^([a-z]{3,})\b/ && Word->new(word => $1)) || ()
        } $_
    } @dicts
]);

while (1) {
    my $letters = prompt 'Enter a set of letters (empty to exit)', '';
    last unless length $letters;
    my @words = sort {length $a <=> length $b || $a cmp $b}
                map  {@{$_->words_spelled($letters)}} @wl;
    printf "%-24s\t%-24s\t%-24s\t%-24s\n", map {shift @words || ''} 1 .. 4
        while @words;
}
