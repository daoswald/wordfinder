#!/usr/bin/env perl

use strict;
use warnings;

use FindBin;

# Within the repo, it's bin/../lib. Elsewhere could be bin/lib.
use lib "$FindBin::Bin/lib";        # bin/lib/
use lib "$FindBin::Bin/../lib";     # bin/../lib/

use File::Slurp;
use IO::Prompt::Tiny 'prompt';
use List::MoreUtils  'uniq';

use Word;
use WordList;

my @dicts = (
#    '/usr/share/dict/words',
#    "$FindBin::Bin/../lib/dict/2of12inf.txt",
    "$FindBin::Bin/../lib/dict/enable1.txt"
);

my @wl = WordList->new(words => [
    uniq
    map {(m/^([a-z]{3,})\b/i && Word->new(word => lc $1)) || ()}
    map {read_file $_, chomp => 1} @dicts
]);

while (1) {
    my $letters = prompt 'Enter a set of letters (empty to exit)', '';
    last unless length $letters;
    my @words = sort {length $a <=> length $b || $a cmp $b}
                map  {@{$_->words_spelled($letters)}} @wl;
    printf "%-10s\t%-10s\t%-10s\t%-10s\n", map {shift @words || ''} 1 .. 4
        while @words;
}
